import os


### Set up build environment

env = Environment(ENV = {'PATH': os.environ['PATH']})
env.Append(CPPPATH = ['/opt/ram/local/include', '/opt/local/include', '#/..', '#include'])
env.Append(LIBPATH = ['/opt/ram/local/lib', '/opt/local/lib'])
env.Append(CPPFLAGS = ['-O3','-Wall','-Werror'])
env.Append(LIBS = ['boost_program_options-mt'])

#env.Replace(CC = 'bfin-linux-uclibc-gcc', CXX = 'bfin-linux-uclibc-g++')
Export('env')


### Build common library

libram_sonar = env.SConscript('src/common.SConscript', variant_dir='#build', duplicate=0)
env = env.Clone()
env.Append(LIBS = [libram_sonar])
Export('env')


### Build sonard, the Blackfin-hosted sonar daemon

sonard = env.SConscript('src/sonard/SConscript', variant_dir='#build/sonard', duplicate=0) 


### Build tools

tools = env.SConscript('src/tools/SConscript', variant_dir='#build/tools', duplicate=0)


### Build and run unit tests

tests = env.SConscript('src/test/SConscript', variant_dir='#build/test', duplicate=0)
